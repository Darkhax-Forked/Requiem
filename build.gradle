// Credits to UpcraftLP for the base build script


import net.fabricmc.loom.task.RemapJarTask

import java.time.Year

plugins {
    id "fabric-loom" version "0.2.5-SNAPSHOT"
    id "net.minecrell.licenser" version "0.4.1"
    id "com.matthewprenger.cursegradle" version "1.1.2"
    id "maven-publish"
}

group = rootProject.maven_group
archivesBaseName = rootProject.name.toLowerCase(Locale.ROOT)
version = mod_version

subprojects {
    apply plugin: 'fabric-loom'
    apply plugin: 'com.matthewprenger.cursegradle'
    apply plugin: 'maven-publish'

    archivesBaseName = "${rootProject.archivesBaseName}-${project.name.toLowerCase(Locale.ROOT)}"
    group = "${rootProject.group}.${rootProject.archivesBaseName}"
}

allprojects {

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    version = rootProject.mod_version

    /*
     Adds a JavadocJar task, javax annotations, manifest attributes, curseforge and maven configuration
     */
    apply from: "https://raw.githubusercontent.com/NerdHubMC/Gradle-Scripts/master/scripts/fabric/basic_project.gradle"

    sourceSets {
        api
    }

    processResources {
        inputs.property "version", project.version

        from(sourceSets.main.resources.srcDirs) {
            include "fabric.mod.json"
            expand "version": project.version
        }

        from(sourceSets.main.resources.srcDirs) {
            exclude "fabric.mod.json"
        }
    }

    repositories {
        maven {
            url "http://maven.fabricmc.net/"
            name "Fabric"
        }
        maven {
            url "https://jitpack.io"
            name "JitPack"
        }
    }

    task apiJar(type: Jar, dependsOn: apiClasses) {
        classifier = 'api-dev'
        from sourceSets.api.allJava
        from (sourceSets.api.output) {
            include "fabric.mod.json"
            expand "version": project.version
        }
        from (sourceSets.api.output) {
            exclude "fabric.mod.json"
        }
    }

    task remapApiJar(type: RemapJarTask, dependsOn: project.apiJar) {
        input = apiJar.archivePath
        classifier = 'api'
        addNestedDependencies = false
    }

    javadoc {
        source += sourceSets.api.allJava
    }

    dependencies {
        //to change the versions see the gradle.properties file
        minecraft "com.mojang:minecraft:${project.minecraft_version}"
        mappings "net.fabricmc:yarn:${project.yarn_mappings}"
        modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

        // Fabric API. This is technically optional, but you probably want it anyway.
        modApi "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
        
        // Lazily evaluated callable returning a non-live (unlike a FileCollection) list of files
        sourceSets.api.compileClasspath += files({sourceSets.main.compileClasspath.toList()})

        compile sourceSets.api.output
        include files(remapApiJar.archivePath)

        compileOnly "org.jetbrains:annotations:${jb_annotations_version}"
        compileOnly "org.apiguardian:apiguardian-api:${apiguardian_version}"

        testImplementation "org.junit.jupiter:junit-jupiter-api:5.4.0-M1"
    }

    remapJar.dependsOn(remapApiJar)

    license {
        header = rootProject.file("code_quality/${project.license_header}_HEADER.txt")

        include "**/*.java"
        exclude "**/package-info.java"

        //export variables
        ext {
            year = Year.now()
            projectDisplayName = rootProject.name
            projectOwners = rootProject.owners
            gplVersion = rootProject.gpl_version
        }

        newLine = false // Disables the empty line between the header and package name
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifact(remapApiJar) {
                    classifier = "api"
                }
            }
        }
    }
}

dependencies {
    modImplementation("com.github.Ladysnake:Satin:${rootProject.satin_version}")
    include "com.github.Ladysnake:Satin:${satin_version}"
    modRuntime "io.github.prospector.modmenu:ModMenu:${modmenu_version}"
    modApi "com.github.Pyrofab.Cardinal-Components-API:cardinal-components-base:${cca_version}"
    include "com.github.Pyrofab.Cardinal-Components-API:cardinal-components-base:${cca_version}"
    modImplementation "com.github.Pyrofab.Cardinal-Components-API:cardinal-components-entity:${cca_version}"
    include "com.github.Pyrofab.Cardinal-Components-API:cardinal-components-entity:${cca_version}"
    modImplementation "com.github.Pyrofab.Cardinal-Components-API:cardinal-components-world:${cca_version}"
    include "com.github.Pyrofab.Cardinal-Components-API:cardinal-components-world:${cca_version}"
}

jar {
    from "LICENSE-CODE"
    from "LICENSE-ART"
}

license {
    exclude "**/RayHelper.java"
}